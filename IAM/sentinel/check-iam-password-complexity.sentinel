import "tfplan-functions" as plan 

pwPolicy = plan.find_resources("aws_iam_account_password_policy")

# (KR) 패스워드는 최소 8자 이상이여야 합니다.
# (EN) Passwords must be at least 8 characters long.
password_length = rule {
    all pwPolicy as _, rc {
      (rc.change.after.minimum_password_length else 0) >= 8
    }
}

# (KR) 소문자 설정은 필수로 활성화 되어야 합니다.
# (EN) The lowercase setting must be enabled by default.
password_lowercase = rule {
    all pwPolicy as _, rc {
      (rc.change.after.require_lowercase_characters else false) is true
    }
}

# (KR) 숫자 설정은 필수로 활성화 되어야 합니다.
# (EN) The numeric setting must be enabled.
password_numbers = rule {
    all pwPolicy as _, rc {
      (rc.change.after.require_numbers else false) is true
    }
}

# (KR) 특수문자 설정은 필수로 활성화 되어야 합니다.
# (EN) The special characters setting must be enabled.
password_symbols = rule {
    all pwPolicy as _, rc {
      (rc.change.after.require_symbols else false) is true
    }
}

# (KR) password_complexity 규칙 여부 확인
# (EN) Check for the password_complexity rule
main = rule {
  password_length and
  password_lowercase and
  password_numbers and
  password_symbols
}